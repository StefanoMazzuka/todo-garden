plugins {
    id("com.android.application")
    kotlin("android")
    kotlin("kapt")
    id("org.jetbrains.kotlin.plugin.compose")
}
android {
    namespace = "com.todogarden"
    compileSdk = 35
    defaultConfig {
        applicationId = "com.todogarden"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "0.1.0"
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = "17" }
    buildFeatures { compose = true }
    sourceSets["main"].assets.srcDir(rootProject.file("assets"))
    sourceSets["main"].jniLibs.srcDir(layout.buildDirectory.dir("generated/gdxNatives"))
    packaging { resources.excludes += "/META-INF/{AL2.0,LGPL2.1}" }
}
val natives by configurations.creating
val extractGdxNatives by tasks.registering(Sync::class) {
    into(layout.buildDirectory.dir("generated/gdxNatives"))
    listOf("armeabi-v7a", "arm64-v8a", "x86", "x86_64").forEach { abi ->
        from({ natives.filter { it.name.endsWith("natives-$abi.jar") }.map { zipTree(it) } }) {
            include("*.so")
            into(abi)
        }
    }
}
tasks.named("preBuild") { dependsOn(extractGdxNatives) }
dependencies {
    implementation(project(":garden"))
    implementation("com.badlogicgames.gdx:gdx:1.14.0")
    implementation("com.badlogicgames.gdx:gdx-backend-android:1.14.0")
    listOf("armeabi-v7a", "arm64-v8a", "x86", "x86_64").forEach {
        natives("com.badlogicgames.gdx:gdx-platform:1.14.0:natives-$it")
    }
    implementation(platform("androidx.compose:compose-bom:2025.04.01"))
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui-tooling-preview")
    debugImplementation("androidx.compose.ui:ui-tooling")
    implementation("androidx.activity:activity-compose:1.10.1")
    implementation("androidx.fragment:fragment-ktx:1.8.6")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.9.0")
    implementation("androidx.room:room-runtime:2.7.1")
    implementation("androidx.room:room-ktx:2.7.1")
    kapt("androidx.room:room-compiler:2.7.1")
}
